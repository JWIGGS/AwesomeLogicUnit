module au_top (
    input clk,              // 100MHz clock
    input rst_n,            // reset button (active low)
    output led [8],         // 8 user controllable LEDs
    input usb_rx,           // USB->Serial input
    output usb_tx,          // USB->Serial output
    output io_led [3][8],   // LEDs on IO Shield
    output io_seg [8],      // 7-segment LEDs on IO Shield
    output io_sel [4],      // Digit select on IO Shield
    input io_button [5],    // 5 buttons on IO Shield
    input io_dip [3][8]    // DIP switches on IO Shield

  ) {

  // reset signal
  sig rst;

  .clk(clk) {
    // The reset conditioner is used to synchronize the reset signal to the FPGA
    // clock. This ensures the entire FPGA comes out of reset at the same time.
    reset_conditioner reset_cond;
  }
  
  //---------------------------------------BUTTONS---------------------------------------//
  
  //variables for my button pressed signal
  dff io_button_pressed[5](.clk(clk), .rst(rst));
  dff io_button_ready[5](.clk(clk), .rst(rst));
  var i;
  
  //---------------------------------------DISPLAY MANAGER---------------------------------------//
  
  //variables for 7 segment display
  counter io_display_counter(#SIZE(2),#DIV(16), .clk(clk), .rst(rst));
  decoder io_display_decoder(#WIDTH(2));

  display_manager io_display_manager;

  
  //---------------------------------------MODES---------------------------------------//
  
  //we have 4 modes:
  //mode 0: for manual testing to input value of A (in A)
  //mode 1: for manual testing to input value of B (in B)
  //mode 2: for manual testing to display the result (out)
  //mode 3: for automatic testing to iterate through all test cases (auto)
  dff mode[2](.clk(clk), .rst(rst));
  
  
  //store values in dffs for 
  dff store_valueA[16](.clk(clk), .rst(rst));
  dff store_valueB[16](.clk(clk), .rst(rst));
  dff store_alufn[6](.clk(clk), .rst(rst));
  dff store_valueOut[16](.clk(clk), .rst(rst));
  
  //---------------------------------------TEST CASE MANAGER---------------------------------------//
  
  test_manager test_case(.clk(clk), .rst(rst));
  
  dff test_case_paused(.clk(clk), .rst(rst));
  
  //create signal variables for checking 
  sig checkResult, checkT, checkZ, checkV, checkN, testPass;
  
  
  
  //---------------------------------------ACTUAL ALU MODULE---------------------------------------//
  
  alu_whole function_alu;
  
  
  always {
  

    //---------------------------------------STANDARD ALWAYS STUFF---------------------------------------//
    
    // input raw inverted reset signal
    reset_cond.in = ~rst_n;

    // conditioned reset
    rst = reset_cond.out;

    // loop serial port
    usb_tx = usb_rx;

    // turn everything off
    led = 8h00;
    io_led = 3x{{8h00}};
    
    
    //---------------------------------------INPUTS FROM BUTTON PRESSES---------------------------------------//
    
    //handle button presses such that io_button_pressed is set to 1 only at the instant the button is actually pressed
    //this is achieved through a ready stats when the button is not pressed
    //when the button is pressed, it becomes not ready (and cannot be pressed again) and the io_button_pressed is set to true, from which we can read from
    //when reading the io_button_pressed.q value, make sure to set the value to 0
    
    for (i = 0; i < 5; i++){
      if(io_button[i]){
        if(io_button_ready.q[i]){
          io_button_ready.d[i] = 0;
          io_button_pressed.d[i] = 1;
        }

      }
      else{
        io_button_ready.d[i] = 1;
        io_button_pressed.d[i] = 0;
      }
    }
    
    
    //---------------------------------------INPUTS FROM BUTTON ACTIONS---------------------------------------//

    //right button press goes to next mode
    if(io_button_pressed.q[Button.RIGHT]){
      io_button_pressed.d[Button.RIGHT] = 0;
      mode.d = mode.q+1;
    }

    //left button press goes to previous mode
    if(io_button_pressed.q[Button.LEFT]){
      io_button_pressed.d[Button.LEFT] = 0;
      mode.d = mode.q-1;
    }

    //middle button press
    if(io_button_pressed.q[Button.MIDDLE]){
      io_button_pressed.d[Button.MIDDLE] = 0;
      
      
      if(mode.q == 3){
        //toggle the pause function
        
        test_case_paused.d = ~test_case_paused.q;
        
      }
      else{
        //save value of switches to value A or B
        case (mode.q){
          0:
            store_valueA.d = $flatten(io_dip[2:1]);
          1:
            store_valueB.d = $flatten(io_dip[2:1]);
        }
  
        //save value of switches to alufn
        store_alufn.d = io_dip[0][5:0];
          
      }
        
    }
    
    
    
    //---------------------------------------INPUTS FROM TEST CASES---------------------------------------//
    
    //feed in the test case whether we are paused or not
    test_case.test_paused = test_case_paused.q;

    
    //---------------------------------------INPUTS FROM ALU---------------------------------------//
    
    case(mode.q){
        
      3:
        //auto mode
        
        //in auto mode, the inputs to the alu are driven by the test cases
        //current test is the value of the current test
        function_alu.a = test_case.valueA;
        function_alu.b = test_case.valueB;
        function_alu.alufn = test_case.alufn;
        
        
        //pass the test case alufn to the display manager
        io_display_manager.alufn = test_case.alufn;
        
        //the results from the alu are checked against the expected results from the test cases
        checkResult = (function_alu.result == test_case.result);
        
        //compare the reuslts of the alu ZVN to the test case ZVN
        checkT = test_case.t;
        checkZ = function_alu.z == test_case.z;
        checkV = function_alu.v == test_case.v;
        checkN = function_alu.n == test_case.n;        
        
        //the results are compiled and sent to the display manager to show PASS or FAIL
        //if check T is true, then the pass check will rely on ZVN checks. if not, then it just relies on the result check.
        testPass = (checkT? (checkResult && checkZ && checkV && checkN): (checkResult));
        
        
        //send the results if ZVN are correct to the display manager
        //so if the Z is correct, it will be shown etc.
        io_display_manager.z = checkZ;
        io_display_manager.v = checkV;
        io_display_manager.n = checkN;
        
        io_display_manager.t = checkT;
      
      default:
        
        //all manual testing modes
        

        //pass the stored values of A, B, and alufn into the ALU
        function_alu.a = store_valueA.q;
        function_alu.b = store_valueB.q;
        function_alu.alufn = store_alufn.q;
        
        //store the result of the alu into the value out dffs
        store_valueOut.d = function_alu.result;
        
        //pass the alufn to the display manager
        io_display_manager.alufn = store_alufn.q;
        
        //set the test pass to zero, indicating failure
        //it doesnt really matter because the test pass is only used for auto testing, so this i just the default
        testPass = 0;
        
        //pass the ZVN values to the display manager so that it can view them
        io_display_manager.z = function_alu.z;
        io_display_manager.v = function_alu.v;
        io_display_manager.n = function_alu.n;
        
        //set the t input to true so that the zvn result is displayed
        io_display_manager.t = 1;
        

    }
    

    //--------------------------------------- OUTPUT LED DISPLAY---------------------------------------//
    
    case(mode.q){
      
      
      0:
        //MODE: a in
        //display the current value of A
        io_led[1] = store_valueA.q[7:0];
        io_led[2] = store_valueA.q[15:8];

        //display the current value of alufn
        io_led[0] = store_alufn.q;
      
      
      1:
        //MODE: b in
        //display the current value of B
        io_led[1] = store_valueB.q[7:0];
        io_led[2] = store_valueB.q[15:8];

        //display the current value of alufn
        io_led[0] = store_alufn.q;
      
      
      2:
        //MODE: out
        //display the current value of the output
        io_led[1] = store_valueOut.q[7:0];
        io_led[2] = store_valueOut.q[15:8];

        //display the current value of alufn
        io_led[0] = store_alufn.q;
      
      
      3:
        //MODE: auto
        //display the current test case as binary on the leds
        //the reason we are doing this weird subtraction is because even though our test cases are counting forward,
        //lucid want to think that the 0th test case is at the bottom of my list, when i want to call 0 to be the top of the list.
        //so instead of reversing the inputs, we are just reversing the output shown so that it matches...
        //with the test case we think we are looking at if we count from the top of the list.
        io_led[0] = TestCases.AMOUNT - 1 - test_case.current_case;
        io_led[1] = testPass? 8hff: 8h00;
        io_led[2] = testPass? 8hff: 8h00;
    }
    
    
    //---------------------------------------OUTPUT DISPLAY MANAGER---------------------------------------//
    
    //our seven segment displays can only be addressed one at a time
    //so in order to assign a value to all of them, we are very quickly looking at each one and giving it a value
    
    
    //the io_display_counter is a 2 bit counter that will iterate through and select values 0, 1, 2 and 3 sequentially
    //we then plug the counter value into a decoder, which will give us a 1000 or 0100 or 0010 or 0001 depending on the value of the counter
    //because the segment display is weird, a 1 means not selected and a 0 means selected, so we invert (~) the output of the decoder
    io_display_decoder.in = io_display_counter.value;
    io_sel = ~io_display_decoder.out;
    
    
    //the display manager determines the value that we want to show provided a few arguments
    //if display toggle is 1, then the values shown are based on the current mode
    //if display toggle is 1, then the values shown are based on the alufn codes 
    io_display_manager.displayToggle = io_button[Button.DOWN];
    io_display_manager.mode = mode.q;
    
    
    //the view ZVN are shown as outputs in the display manager
    io_display_manager.view_zvn = io_button[Button.UP];
    
    //because the segment display is weird, 1 means that the segment is turned off, so we invert (~) the output of the display
    //the segment we are assigning is based on the display counter value such that when a segment is selected, we are writing that value of the segment as well
    io_seg = ~io_display_manager.display_output[io_display_counter.value];
    

    

    
    
    







  }
}
